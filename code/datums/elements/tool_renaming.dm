#define OPTION_RENAME "Название"
#define OPTION_DESCRIPTION "Описание"
#define OPTION_RESET "Сбросить"

/**
 * Renaming tool element
 *
 * When using this tool on an object with UNIQUE_RENAME,
 * lets the user rename/redesc it.
 */
/datum/element/tool_renaming

/datum/element/tool_renaming/Attach(datum/target)
	. = ..()
	if(!isitem(target))
		return ELEMENT_INCOMPATIBLE

	RegisterSignal(target, COMSIG_ITEM_INTERACTING_WITH_ATOM, PROC_REF(attempt_rename))

/datum/element/tool_renaming/Detach(datum/source)
	. = ..()
	UnregisterSignal(source, COMSIG_ITEM_INTERACTING_WITH_ATOM)

/datum/element/tool_renaming/proc/attempt_rename(datum/source, mob/living/user, atom/interacting_with, list/modifiers)
	SIGNAL_HANDLER

	if(!isobj(interacting_with))
		return NONE

	var/obj/renamed_obj = interacting_with
	var/obj/item/tool = source

	if(!(renamed_obj.obj_flags & UNIQUE_RENAME) || !user.can_write(tool))
		return NONE
	INVOKE_ASYNC(src, PROC_REF(async_rename), user, renamed_obj, !(renamed_obj.obj_flags & RENAME_NO_DESC))
	return ITEM_INTERACT_SUCCESS

/datum/element/tool_renaming/proc/async_rename(mob/living/user, obj/renamed_obj, description_option)
	if(!renamed_obj.rename_checks(user))
		return
	var/custom_choice = tgui_input_list(user, "Что вы хотите изменить?", "Подпись", list(OPTION_RENAME, description_option? OPTION_DESCRIPTION : null, OPTION_RESET))
	if(QDELETED(renamed_obj) || !user.can_perform_action(renamed_obj) || isnull(custom_choice))
		return

	switch(custom_choice)
		if(OPTION_RENAME)
			var/old_name = renamed_obj.name
			var/input = tgui_input_text(user, "Какое название дать этому предмету?", "Название предмета", "[old_name]", MAX_NAME_LEN)
			if(QDELETED(renamed_obj) || !user.can_perform_action(renamed_obj))
				return
			if(input == old_name || !input)
				to_chat(user, span_notice("Вы меняете название на... ну... то же самое."))
				return
			renamed_obj.AddComponent(/datum/component/rename, renamed_obj.nameformat(input, user), renamed_obj.desc)
			to_chat(user, span_notice("Теперь это называется так: [renamed_obj]."))
			renamed_obj.update_appearance(UPDATE_NAME)

		if(OPTION_DESCRIPTION)
			var/old_desc = renamed_obj.desc
			var/input = tgui_input_text(user, "Опишите этот предмет", "Описание", "[old_desc]", MAX_DESC_LEN)
			if(QDELETED(renamed_obj) || !user.can_perform_action(renamed_obj))
				return
			if(input == old_desc || !input)
				to_chat(user, span_notice("Вы решаете не менять описание."))
				return
			renamed_obj.AddComponent(/datum/component/rename, renamed_obj.name, renamed_obj.descformat(input, user))
			to_chat(user, span_notice("Вы меняете описание предмета."))
			renamed_obj.update_appearance(UPDATE_DESC)

		if(OPTION_RESET)
			qdel(renamed_obj.GetComponent(/datum/component/rename))
			to_chat(user, span_notice("Вы возвращаете предмету прежнее название[renamed_obj.obj_flags & RENAME_NO_DESC? "." : " и описание."]"))
			renamed_obj.rename_reset()
			renamed_obj.update_appearance(UPDATE_NAME | UPDATE_DESC)

#undef OPTION_RENAME
#undef OPTION_DESCRIPTION
#undef OPTION_RESET
