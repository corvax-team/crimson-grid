/**
 * # secret documents
 *
 * Indestructible antag objective that can be photocopied.
 *
 * Photocopying this is handled in photocopier.dm.
 * Cannot be destroyed, but can be spaced.
 * Save for the inhand, this does not actually have anything in common with /obj/item/paper.
*/
/obj/item/documents
	name = "secret documents"
	desc = "Документы под грифом \"Совершенно секретно\"."
	icon = 'icons/obj/service/bureaucracy.dmi'
	icon_state = "docs_generic"
	inhand_icon_state = "paper"
	throwforce = 0
	w_class = WEIGHT_CLASS_TINY
	throw_range = 1
	throw_speed = 1
	layer = MOB_LAYER
	pressure_resistance = 2
	resistance_flags = INDESTRUCTIBLE | LAVA_PROOF | FIRE_PROOF | ACID_PROOF
	// Could use a more specific sound since it's a lot of paper (ditto with `/paperwork`).
	drop_sound = 'sound/items/handling/paper_drop.ogg'
	pickup_sound = 'sound/items/handling/paper_pickup.ogg'

///Nanotrasen documents
/obj/item/documents/nanotrasen
	desc = "Документы с грифом \"Совершенно секретно\": сложные схемы и списки имён, дат и координат."
	icon_state = "docs_verified"

///Syndicate documents
/obj/item/documents/syndicate
	desc = "\"Top Secret\" documents detailing sensitive Syndicate operational intelligence."

///Syndicate documents with a red seal
/obj/item/documents/syndicate/red
	name = "red secret documents"
	desc = "\"Top Secret\" documents detailing sensitive Syndicate operational intelligence. These documents are verified with a red wax seal."
	icon_state = "docs_red"

///Syndicate documents with a blue seal
/obj/item/documents/syndicate/blue
	name = "blue secret documents"
	desc = "\"Top Secret\" documents detailing sensitive Syndicate operational intelligence. These documents are verified with a blue wax seal."
	icon_state = "docs_blue"

///Syndicate mining documents
/obj/item/documents/syndicate/mining
	desc = "\"Top Secret\" documents detailing Syndicate plasma mining operations."

/**
 * # secret documents (photocopy)
 *
 * Outcome of photocopying documents. Can be copied, and can have a blue/red seal forged.
*/
/obj/item/documents/photocopy
	desc = "Копия совершенно секретных документов. Никто ведь не заметит, что это не оригинал... правда?"
	///What seal was forged on the documents (color name string)
	var/forgedseal = 0
	///What was copied
	var/copy_type = null

/obj/item/documents/photocopy/Initialize(mapload, obj/item/documents/copy=null)
	. = ..()
	AddComponent(/datum/component/selling, 1, "documents", TRUE) // CRIMSON EDIT ADD - Sell Valuables
	if(copy)
		copy_type = copy.type
		if(istype(copy, /obj/item/documents/photocopy)) // Copy Of A Copy Of A Copy
			var/obj/item/documents/photocopy/C = copy
			copy_type = C.copy_type

/obj/item/documents/photocopy/item_interaction(mob/living/user, obj/item/tool, list/modifiers)
	if(!istype(tool, /obj/item/toy/crayon/red) && !istype(tool, /obj/item/toy/crayon/blue))
		return NONE
	if (forgedseal)
		to_chat(user, span_warning("Печать на этих документах вы уже подделали!"))
		return ITEM_INTERACT_BLOCKING

	var/obj/item/toy/crayon/C = tool
	name = "[C.crayon_color] secret documents"
	icon_state = "docs_[C.crayon_color]"
	forgedseal = C.crayon_color
	to_chat(user, span_notice("Вы подделываете официальную печать мелком. Никто ведь не заметит... правда?"))
	update_appearance()
	return ITEM_INTERACT_SUCCESS

// CRIMSON EDIT ADD START - Sell Valuables
/obj/item/documents/Initialize(mapload)
	. = ..()
	AddComponent(/datum/component/selling, 1000, "documents", TRUE)
// CRIMSON EDIT ADD END - Sell Valuables
