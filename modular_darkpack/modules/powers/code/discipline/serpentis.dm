/datum/discipline/serpentis
	name = "Серпентис"
	desc = {"Уподобьтесь кобре: цепенящий взгляд, разящий язык и древние обычаи бальзамировщиков, которые вы чтите и передаёте своим потомкам. Нарушает Маскарад.
● Глаза змеи: пассивно
●● Язык аспида: Сила (сложность 6)
●●● Шкура гадюки: пассивно
●●●● Облик кобры: пассивно
●●●●● Сердце тьмы: пассивно"}
	icon_state = "serpentis"
	clan_restricted = TRUE
	power_type = /datum/discipline_power/serpentis
	signature_clan = VAMPIRE_CLAN_SETITE

/datum/discipline_power/serpentis
	name = "Serpentis power name"
	desc = "Serpentis power description"

	activate_sound = 'modular_darkpack/modules/powers/sounds/serpentis.ogg'

//THE EYES OF THE SERPENT
/datum/discipline_power/serpentis/the_eyes_of_the_serpent
	name = "Глаза змеи"
	desc = "Ваши глаза обретают гипнотическую силу змеиных: всякий, кто в них заглянет, застывает на месте."

	level = 1
	check_flags = DISC_CHECK_CAPABLE | DISC_CHECK_SEE
	target_type = TARGET_LIVING
	range = 3
	vitae_cost = 0

	aggravating = FALSE
	hostile = FALSE
	violates_masquerade = TRUE
	frenzy_usable = FALSE

	multi_activate = TRUE
	duration_length = 5 SECONDS
	cooldown_length = 5 SECONDS

/datum/discipline_power/serpentis/the_eyes_of_the_serpent/proc/immobilize_target(mob/living/target, duration = 5 SECONDS)
	ADD_TRAIT(target, TRAIT_IMMOBILIZED, DISCIPLINE_TRAIT(type))
	RegisterSignals(target, list(COMSIG_ATOM_ATTACKBY, COMSIG_MOB_ITEM_ATTACK, COMSIG_PROJECTILE_PREHIT), PROC_REF(on_target_attacked))
	if(do_after(owner, duration, target))
		release_target(target)
		return TRUE
	else
		release_target(target)
		return FALSE

/datum/discipline_power/serpentis/the_eyes_of_the_serpent/proc/on_target_attacked(datum/source)
	SIGNAL_HANDLER
	var/mob/living/target = source
	release_target(target)
	to_chat(owner, span_warning("На [target.declent_ru(ACCUSATIVE)] напали - ваша сосредоточенность нарушена!"))
	to_chat(target, span_warning("На вас напали - чужая власть над вашим разумом рушится!"))

/datum/discipline_power/serpentis/the_eyes_of_the_serpent/proc/release_target(mob/living/target)
	UnregisterSignal(target, list(COMSIG_ATOM_ATTACKBY, COMSIG_MOB_ITEM_ATTACK, COMSIG_PROJECTILE_PREHIT))
	to_chat(target, span_danger("Мысли снова принадлежат вам: теперь можно отвести глаза от властного взгляда."))
	REMOVE_TRAIT(target, TRAIT_IMMOBILIZED, DISCIPLINE_TRAIT(type))

/datum/discipline_power/serpentis/the_eyes_of_the_serpent/can_activate_untargeted(alert)
	. = ..()
	if (owner?.is_eyes_covered())
		if (alert)
			to_chat(owner, span_warning("С закрытыми глазами [name] не применить!"))
		. = FALSE
	return .

/datum/discipline_power/serpentis/the_eyes_of_the_serpent/activate(mob/living/target)
	. = ..()
	target.face_atom(owner)
	target.visible_message(span_hypnophrase("<b>[owner] гипнотизирует [target.declent_ru(ACCUSATIVE)] взглядом!</b>"), span_warning("<b>[owner] гипнотизирует вас! Каждое слово звучит всё убедительнее и завораживает...</b>"))
	if(ishuman(target))
		var/mob/living/carbon/human/H = target
		H.remove_overlay(POWERS_LAYER)
		var/mutable_appearance/serpentis_overlay = mutable_appearance('modular_darkpack/modules/powers/icons/serpentis.dmi', "serpentis", -POWERS_LAYER)
		H.overlays_standing[POWERS_LAYER] = serpentis_overlay
		H.apply_overlay(POWERS_LAYER)
	immobilize_target(target)

/datum/discipline_power/serpentis/the_eyes_of_the_serpent/deactivate(mob/living/target)
	. = ..()
	release_target(target)
	if (ishuman(target))
		var/mob/living/carbon/human/human_target = target
		human_target.remove_overlay(POWERS_LAYER)

//THE TONGUE OF THE ASP
/datum/discipline_power/serpentis/the_tongue_of_the_asp
	name = "Язык аспида"
	desc = "Ваш язык удлиняется и жалит врагов, высасывая их кровь."
	level = 2
	check_flags = DISC_CHECK_CAPABLE | DISC_CHECK_IMMOBILE | DISC_CHECK_LYING
	target_type = TARGET_LIVING
	range = 2//CRIMSON EDIT LOWERED RANGE
	effect_sound = 'modular_darkpack/modules/powers/sounds/tongue.ogg'
	aggravating = TRUE
	hostile = TRUE
	violates_masquerade = TRUE
	cooldown_length = 5 SECONDS
	vitae_cost = 0
	var/successes

/datum/discipline_power/serpentis/the_tongue_of_the_asp/can_activate_untargeted(alert)
	. = ..()
	if (owner?.is_mouth_covered())
		if (alert)
			to_chat(owner, span_warning("С закрытым ртом [name] не применить!"))
		. = FALSE
	return .

/datum/discipline_power/serpentis/the_tongue_of_the_asp/pre_activation_checks(mob/living/target)
	. = ..()
	successes = SSroll.storyteller_roll_datum(owner, applic_stats = list(STAT_STRENGTH), numerical = TRUE)
	if(successes > 0)
		return TRUE
	else
		return FALSE

/datum/discipline_power/serpentis/the_tongue_of_the_asp/activate(mob/living/target)
	. = ..()
	target.adjust_blood_pool(-2)
	target.apply_damage(5 * successes, AGGRAVATED)//CRIMSON GRID CHANGE LOWERED DAMAGE
	owner.adjust_blood_pool(2)
	var/obj/item/ammo_casing/magic/tentacle/casing = new (get_turf(owner))
	casing.fire_casing(target, owner, null, null, null, ran_zone(), 0,  owner)
	qdel(casing)

//THE SKIN OF THE ADDER
/datum/discipline_power/serpentis/the_skin_of_the_adder
	name = "Шкура гадюки"
	desc = "Уподобьтесь змее: кожа твердеет и покрывается чешуёй."
	level = 3
	check_flags = DISC_CHECK_CAPABLE | DISC_CHECK_IMMOBILE | DISC_CHECK_LYING
	toggled = TRUE
	vitae_cost = 0 //handling blood cost in pre_activation because this power asks for one bloodpoint, but can be on forever without consuming more
	violates_masquerade = FALSE
	var/choice

/datum/discipline_power/serpentis/the_skin_of_the_adder/pre_activation_checks()
	. = ..()
	owner.adjust_blood_pool(-1)

/datum/discipline_power/serpentis/the_skin_of_the_adder/activate()
	. = ..()
	//this needs a sprite
	choice = tgui_alert(owner, "Насколько заметной будет чешуя на вашем теле?", "Чешуя", list("Едва заметной", "Явной"))
	if(choice == "Явной")
		owner.st_add_stat_mod(STAT_INTIMIDATION, 2, "Serpentis") // 'reduce intimidation difficulties by two' placeholder
		owner.st_add_stat_mod(STAT_STAMINA, 3, "Serpentis") // 'reduces all soak difficulty to 5' placeholder
		ADD_TRAIT(owner, TRAIT_MASQUERADE_VIOLATING_FACE, DISCIPLINE_TRAIT(type))
	else
		owner.st_add_stat_mod(STAT_STAMINA, 2, "Serpentis") // permanently on with no downsides according to dav20. its staying at fort one bro
	ADD_TRAIT(owner, TRAIT_SERPENTIS_SKIN, DISCIPLINE_TRAIT(type)) //ideally this would either be blatantly obvious or not so much depending on the choice. I guess masq violating face trait will work for obvious.
	owner.st_add_stat_clamp(STAT_APPEARANCE, 0, "Serpentis")
	/*
	owner.Stun(duration_length)
	owner.petrify(duration_length, "Serpentis")
	*/

/datum/discipline_power/serpentis/the_skin_of_the_adder/deactivate()
	. = ..()
	if(choice == "Явной")
		owner.st_remove_stat_mod(STAT_INTIMIDATION, 2, "Serpentis")
		owner.st_remove_stat_mod(STAT_STAMINA, 3, "Serpentis")
		REMOVE_TRAIT(owner, TRAIT_MASQUERADE_VIOLATING_FACE, DISCIPLINE_TRAIT(type))
	else
		owner.st_remove_stat_mod(STAT_STAMINA, 2, "Serpentis")
	REMOVE_TRAIT(owner, TRAIT_SERPENTIS_SKIN, DISCIPLINE_TRAIT(type))
	owner.st_remove_stat_clamp(STAT_APPEARANCE, "Serpentis")


//THE FORM OF THE COBRA
/datum/discipline_power/serpentis/the_form_of_the_cobra
	name = "Облик кобры"
	desc = "Обернитесь огромной чёрной коброй и растерзайте врагов."
	level = 4
	check_flags = DISC_CHECK_IMMOBILE | DISC_CHECK_CAPABLE | DISC_CHECK_LYING
	vitae_cost = 1
	violates_masquerade = TRUE
	cancelable = TRUE
	toggled = TRUE
	duration_length = 0
	cooldown_length = 30 SECONDS
	var/datum/action/cooldown/spell/shapeshift/cobra/cobra_form

/datum/discipline_power/serpentis/the_form_of_the_cobra/pre_activation_checks()
	. = ..()
	if(do_after(owner, 4 SECONDS))
		return TRUE
	else
		return FALSE

/datum/discipline_power/serpentis/the_form_of_the_cobra/activate()
	. = ..()
	if(cobra_form)
		CRASH("[src] somehow already has a spell?")

	owner.drop_all_held_items()
	cobra_form = new(owner.mind)
	cobra_form.Grant(owner)
	cobra_form.Activate(owner)
	RegisterSignal(owner, COMSIG_LIVING_RETURNED_FROM_SHAPESHIFT, PROC_REF(deactivate))

/datum/discipline_power/serpentis/the_form_of_the_cobra/deactivate()
	UnregisterSignal(owner, COMSIG_LIVING_RETURNED_FROM_SHAPESHIFT)
	. = ..()
	cobra_form.Remove(owner)
	QDEL_NULL(cobra_form)
	owner.Stun(1.5 SECONDS)
	owner.do_jitter_animation(3 SECONDS)

/datum/action/cooldown/spell/shapeshift/cobra
	name = "Облик кобры"
	desc = "Примите облик зверя."
	button_icon = 'modular_darkpack/modules/vampire_the_masquerade/icons/vampire_clans.dmi'
	button_icon_state = "setite"
	background_icon = 'modular_darkpack/master_files/icons/mob/actions/backgrounds.dmi'
	background_icon_state = "bg_discipline"
	overlay_icon_state = null
	spell_requirements = NONE
	cooldown_time = 5 SECONDS
	revert_on_death = TRUE
	die_with_shapeshifted_form = FALSE
	possible_shapes = list(/mob/living/basic/cobra,
	/mob/living/basic/cobra/typhon)

/mob/living/basic/cobra
	name = "cobra form"
	desc = "Шссссс..."
	icon = 'modular_darkpack/modules/deprecated/icons/48x48.dmi'
	icon_state = "cobra"
	icon_living = "cobra"
	mob_biotypes = MOB_ORGANIC | MOB_HUMANOID
	speed = -1
	maxHealth = 300
	health = 300
	melee_damage_lower = 30
	melee_damage_upper = 30
	attack_verb_continuous = "slashes"
	attack_verb_simple = "slash"
	attack_sound = 'sound/items/weapons/slash.ogg'
	pixel_w = -8

/mob/living/basic/cobra/typhon
	name = "Typhonic beast"
	desc = "Громадный потусторонний шакал: длинные заострённые уши, жёсткий раздвоенный хвост и вытянутая морда."
	icon = 'modular_darkpack/modules/deprecated/icons/icons.dmi'
	icon_state = "protean4"
	icon_living = "protean4"
	mob_size = MOB_SIZE_LARGE
	pixel_w = 0
	initial_size = 1.4

/mob/living/basic/cobra/typhon/Life(seconds_per_tick)
	. = ..()
	SEND_SIGNAL(src, COMSIG_MASQUERADE_VIOLATION)

//THE HEART OF DARKNESS
/datum/discipline_power/serpentis/the_heart_of_darkness
	name = "Сердце тьмы"
	desc = "Извлеките своё сердце и спрячьте в урну - так до него не доберутся ни кол, ни диаблерист."

	level = 5
	check_flags = DISC_CHECK_CAPABLE | DISC_CHECK_IMMOBILE | DISC_CHECK_LYING | DISC_CHECK_FREE_HAND
	vitae_cost = 0

	violates_masquerade = TRUE
	frenzy_usable = FALSE

	cooldown_length = 20 SECONDS

	var/obj/item/urn/urn

/datum/discipline_power/serpentis/the_heart_of_darkness/activate()
	. = ..()
	if(!urn)
		if(owner.dna?.species)
			owner.dna.species.inherent_traits |= TRAIT_STUNIMMUNE
			owner.dna.species.inherent_traits |= TRAIT_SLEEPIMMUNE
			owner.dna.species.inherent_traits |= TRAIT_NOSOFTCRIT
			ADD_TRAIT(owner, TRAIT_STAKE_IMMUNE, DISCIPLINE_TRAIT(type))
			urn = new(owner.loc)
			urn.own = owner
			//var/obj/item/organ/heart/heart = owner.get_organ_slot(ORGAN_SLOT_HEART) DARKPACK TODO - Vampire Organs need to be made useless
			//heart.forceMove(urn)
	else
		if(owner.dna?.species)
			owner.dna.species.inherent_traits -= TRAIT_STUNIMMUNE
			owner.dna.species.inherent_traits -= TRAIT_SLEEPIMMUNE
			owner.dna.species.inherent_traits -= TRAIT_NOSOFTCRIT
			REMOVE_TRAIT(owner, TRAIT_STAKE_IMMUNE, DISCIPLINE_TRAIT(type))
			//for(var/obj/item/organ/heart/heart in urn)
				//heart.forceMove(owner)
				//heart.Insert(owner)
		urn.own = null
		qdel(urn)
		urn = null

/obj/item/urn
	name = "organ urn"
	desc = "В ней хранится кое-что из драгоценных органов..."
	icon = 'modular_darkpack/modules/powers/icons/serpentis.dmi'
	icon_state = "urn"
	var/mob/living/own

/obj/item/urn/attackby(obj/item/I, mob/living/user, params)
	. = ..()
	qdel(src)

/obj/item/urn/attack_self(mob/user)
	. = ..()
	qdel(src)

/obj/item/urn/Destroy()
	. = ..()
	if(own)
		own.death()
