// alot of these npcs, like 'bacotell' 'bubway' and 'endronsecurity_2' should probably not be available to be taken since they're so specialized.
GLOBAL_LIST_INIT(prey_exclusion_choice, list(
	"Middle-income" = /mob/living/carbon/human/npc/incel,
	"Police Officers" = /mob/living/carbon/human/npc/police,
	"Criminals" = /mob/living/carbon/human/npc/bandit,
	"High income" = /mob/living/carbon/human/npc/business,
	//"Strippers" = /mob/living/carbon/human/npc/stripper, i feel like strippers would be the most powergamed option, keeping it here but commented.
	"Homeless" = /mob/living/carbon/human/npc/hobo,
))

/datum/quirk/darkpack/prey_exclusion
	name = "Prey Exclusion"
	ru_name = "Запретная добыча"
	desc = "Вы отказываетесь охотиться на добычу определённого рода. При входе в игру нужно выбрать тип NPC, кровью которых вы не сможете питаться. Вентру не могут взять этот недостаток."
	ttrpg_sources = list(/datum/source_book/vtm20 = 485)
	value = -1
	mob_trait = TRAIT_PREY_EXCLUSION
	gain_text = span_notice("Вы становитесь очень разборчивы в том, чьей кровью питаться.")
	lose_text = span_notice("Вам больше нет дела до того, чьей кровью питаться.")
	allowed_splats = list(SPLAT_KINDRED)
	excluded_clans = list(VAMPIRE_CLAN_VENTRUE)
	icon = FA_ICON_FACE_FROWN
	failure_message = "Вам больше нет дела до того, чьей кровью питаться."
	/// which type of prey the user selected
	var/prey_exclusion
	quirk_flags = QUIRK_HIDE_FROM_SCAN //CRIMSON GRID EDIT ADD | PR: MAKE MEDICAL RECORDS NOT MASQ BREACHY | CHANGE: ADDED THIS TO PREVENT IT FROM BEING SEEN IN COMS

/datum/quirk/darkpack/prey_exclusion/add(client/client_source)
	prey_exclusion = GLOB.prey_exclusion_choice[client_source?.prefs.read_preference(/datum/preference/choiced/prey_exclusion)] || /mob/living/carbon/human/npc/hobo

/datum/quirk_constant_data/prey_exclusion
	associated_typepath = /datum/quirk/darkpack/prey_exclusion
	customization_options = list(/datum/preference/choiced/prey_exclusion)

/datum/preference/choiced/prey_exclusion
	category = PREFERENCE_CATEGORY_MANUALLY_RENDERED
	savefile_key = "prey_exclusion"
	savefile_identifier = PREFERENCE_CHARACTER

/datum/preference/choiced/prey_exclusion/init_possible_values()
	return GLOB.prey_exclusion_choice

/datum/preference/choiced/prey_exclusion/create_default_value()
	return "Homeless"

/datum/preference/choiced/prey_exclusion/compile_constant_data()
	var/list/data = ..()
	data[CHOICED_PREFERENCE_DISPLAY_NAMES] = list(
		"Middle-income" = "Средний класс",
		"Police Officers" = "Полицейские",
		"Criminals" = "Преступники",
		"High income" = "Богачи",
		"Homeless" = "Бездомные",
	)
	return data

/datum/preference/choiced/prey_exclusion/is_accessible(datum/preferences/preferences)
	. = ..()
	if (!.)
		return FALSE

	return /datum/quirk/darkpack/prey_exclusion::name in preferences.all_quirks

/datum/preference/choiced/prey_exclusion/apply_to_human(mob/living/carbon/human/target, value, datum/preferences/preferences)
	var/datum/quirk/darkpack/prey_exclusion/quirk = target.get_quirk(/datum/quirk/darkpack/prey_exclusion)
	if(!quirk)
		return
	quirk.prey_exclusion = GLOB.prey_exclusion_choice[value] || /mob/living/carbon/human/npc/hobo
