GLOBAL_LIST_INIT(territorial_type_choices, init_territorial_type_choices())

/proc/init_territorial_type_choices()
	var/list/choices = list()
	for(var/area/vtm/area_type as anything in subtypesof(/area/vtm))
		var/area/vtm/typed = area_type
		var/area/vtm/parent = area_type::parent_type
		if(initial(typed.domain) && !initial(parent.domain))
			choices[initial(typed.name)] = area_type
	return choices

/datum/preference/choiced/territorial
	category = PREFERENCE_CATEGORY_MANUALLY_RENDERED
	savefile_key = "territorial"
	savefile_identifier = PREFERENCE_CHARACTER

/datum/preference/choiced/territorial/init_possible_values()
	return GLOB.territorial_type_choices

/datum/preference/choiced/territorial/create_default_value()
	return /area/vtm/outside/financialdistrict::name

/datum/preference/choiced/territorial/compile_constant_data()
	var/list/data = ..()
	var/list/display_names = list()
	for(var/area_name in data["choices"])
		display_names[area_name] = capitalize(declent_ru_initial(area_name, NOMINATIVE, area_name))
	data[CHOICED_PREFERENCE_DISPLAY_NAMES] = display_names
	return data

/datum/preference/choiced/territorial/is_accessible(datum/preferences/preferences)
	. = ..()
	if (!.)
		return FALSE
	return /datum/quirk/darkpack/territorial::name in preferences.all_quirks

/datum/preference/choiced/territorial/apply_to_human(mob/living/carbon/human/target, value, datum/preferences/preferences)
	var/datum/quirk/darkpack/territorial/terr = target.get_quirk(/datum/quirk/darkpack/territorial)
	if(!terr)
		return
	terr.territory = GLOB.territorial_type_choices[value] || /area/vtm/outside/financialdistrict

/datum/quirk/darkpack/territorial
	name = "Territorial"
	ru_name = "Территориальность"
	desc = "Вы ревностно охраняете свою территорию и можете кормиться только в одном районе. Если другой вампир зайдёт на вашу территорию без спроса, вы встретите его враждебно, а если он покормится там без разрешения, дело наверняка дойдёт до драки: он отнимает вашу еду и ресурсы. Без необходимости вы стараетесь не покидать свою территорию. Кормиться вы можете только на ней."
	ttrpg_sources = list(/datum/source_book/vtm20 = 486)
	value = -2
	mob_trait = TRAIT_VAMPIRE_TERRITORIAL
	gain_text = span_notice("Вы должны защищать свои охотничьи угодья, своё стадо, свою территорию.")
	lose_text = span_notice("Да какая разница, кто где кормится?")
	allowed_splats = list(SPLAT_KINDRED)
	icon = FA_ICON_MAP_LOCATION_DOT
	failure_message = "Да какая разница, кто где кормится?"
	var/territory
	quirk_flags = QUIRK_HIDE_FROM_SCAN //CRIMSON GRID EDIT ADD | PR: MAKE MEDICAL RECORDS NOT MASQ BREACHY | CHANGE: ADDED THIS TO PREVENT IT FROM BEING SEEN IN COMS

/datum/quirk_constant_data/territorial
	associated_typepath = /datum/quirk/darkpack/territorial
	customization_options = list(/datum/preference/choiced/territorial)

/datum/quirk/darkpack/territorial/add(client/client_source)
	territory = GLOB.territorial_type_choices[client_source?.prefs.read_preference(/datum/preference/choiced/territorial)] || /area/vtm/outside/financialdistrict
