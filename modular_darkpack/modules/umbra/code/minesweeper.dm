/datum/storyteller_roll/mindsweeper
	applicable_stats = list(STAT_PERCEPTION, STAT_OCCULT)

/obj/minespot
	name = "safe umbral tether"
	desc = "Связывает части Пенумбры между собой."
	icon = 'modular_darkpack/modules/umbra/icons/umbra.dmi'
	icon_state = "tile1"
	plane = GAME_PLANE
	layer = BELOW_OBJ_LAYER
	resistance_flags = INDESTRUCTIBLE | LAVA_PROOF | FIRE_PROOF | UNACIDABLE | ACID_PROOF | FREEZE_PROOF
	anchored = TRUE
	density = TRUE
	var/marked = FALSE
	var/bomb = FALSE
	var/uncovered = FALSE
	var/amount_of_bombs
	var/dangerous = FALSE

/obj/minespot/Initialize(mapload)
	. = ..()
	AddElement(/datum/element/contextual_screentip_bare_hands, lmb_text = "Открыть", rmb_text = "Пометить")
	color = "#8e8e8e"
	icon_state = "tile[rand(1, 16)]"

/obj/minespot/examine(mob/user)
	. = ..()
	if(!bomb && uncovered)
		. += "Опасных привязей рядом: [amount_of_bombs]"

/obj/minespot/proc/uncover(mob/living/user)
	if(uncovered || marked)
		return FALSE
	uncovered = TRUE
	density = FALSE
	if(bomb)
		animate(src, color = "#FFFFFF", time = 1 SECONDS)
		icon_state = "boom"
		if(!dangerous)
			return
		var/datum/storyteller_roll/mindsweeper/perc_roll = new()
		var/roll_result = perc_roll.st_roll(user, src)
		switch(roll_result)
			if(ROLL_SUCCESS)
				to_chat(user, span_revenwarning("Чуть не попались... но на этот раз духи вас не карают."))
			if(ROLL_FAILURE)
				to_chat(user, span_revendanger("Слишком близко... Разум охватывает тревога."))
				user.adjust_agg_loss(5)
			if(ROLL_BOTCH)
				to_chat(user, span_revendanger("ЗА ЭТО ДУХИ ВАС КАРАЮТ."))
				user.adjust_agg_loss(25)
		return
	amount_of_bombs = nearby_mines()
	switch(amount_of_bombs)
		if(0)
			animate(src, color = "#FFFFFF", time = 1 SECONDS)
		if(1)
			animate(src, color = "#00edff", time = 1 SECONDS)
		if(2)
			animate(src, color = "#40ff00", time = 1 SECONDS)
		if(3)
			animate(src, color = "#ffbf00", time = 1 SECONDS)
		if(4)
			animate(src, color = "#ff0000", time = 1 SECONDS)
		if(5)
			animate(src, color = "#ff0089", time = 1 SECONDS)
		if(6)
			animate(src, color = "#c800ff", time = 1 SECONDS)
		if(7)
			animate(src, color = "#4000ff", time = 1 SECONDS)
		if(8)
			animate(src, color = "#4000ff", time = 1 SECONDS)
	if(!bomb && amount_of_bombs == 0)
		for(var/obj/minespot/M in range(1, src))
			M.uncover(user)
	return TRUE

/obj/minespot/attack_hand(mob/living/user, list/modifiers)
	. = ..()
	return uncover(user)

/obj/minespot/Bumped(atom/movable/bumped_atom)
	if(!dangerous && isliving(bumped_atom))
		uncover(bumped_atom)
	. = ..()

/obj/minespot/proc/nearby_mines()
	var/nearby_bombs = 0
	for(var/obj/minespot/M in range(1, src))
		if(M.bomb)
			nearby_bombs += 1
	return clamp(nearby_bombs, 0, 8)

/obj/minespot/attack_hand_secondary(mob/living/user, list/modifiers)
	. = ..()
	if(!uncovered)
		if(!marked)
			icon_state = "marked"
			marked = TRUE
		else
			icon_state = "tile[rand(1, 16)]"
			marked = FALSE
	return SECONDARY_ATTACK_CANCEL_ATTACK_CHAIN

/obj/minespot/playable
	name = "umbral tether"

/obj/minespot/playable/Initialize(mapload)
	. = ..()
	if(prob(20))
		bomb = TRUE

/obj/minespot/playable/dangerous
	name = "unforgiving umbral tether"
	dangerous = TRUE
