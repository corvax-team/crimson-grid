GAME_VERB(/mob, do_verb, "Do", null)
	if(GLOB.say_disabled) // This is here to try to identify lag problems
		to_chat(usr, span_danger("Общение было заблокировано администрацией."))
		return
	DEFAULT_QUEUE_OR_CALL_VERB(VERB_CALLBACK(src, PROC_REF(emote), "do_emote"))

/datum/emote/living/do_emote
	key = "do_emote"
	key_third_person = "do_emote"
	message = null
	mob_type_blacklist_typecache = list(/mob/living/brain)

/datum/emote/living/do_emote/run_emote(mob/user, params, type_override, intentional)
	if(!can_run_emote(user))
		to_chat(user, span_warning("Сейчас вы не можете использовать эмоции."))
		return FALSE
	if(SSdbcore.IsConnected() && is_banned_from(user, "Emote"))
		to_chat(user, span_warning("Вам запрещено использовать эмоции (бан)."))
		return FALSE
	else if(user.client?.prefs.muted & MUTE_IC)
		to_chat(user, span_warning("Вы не можете отправлять IC-сообщения (мут)."))
		return FALSE

	var/message = tgui_input_text(user, "Опишите, что происходит вокруг вашего персонажа.", "Описание сцены (Do)", null, max_length = MAX_MESSAGE_LEN, multiline = TRUE)
	if (!message)
		return

	if (!user.try_speak(message)) // ensure we pass the vibe check (filters, etc)
		return

	var/name_stub = " (<b>[user.name]</b>)"
	message = trim(copytext_char(message, 1, (MAX_MESSAGE_LEN - length(name_stub))))
	var/message_with_name = message + name_stub

	user.log_message(message, LOG_EMOTE)

	var/list/viewers = get_hearers_in_view(DEFAULT_MESSAGE_RANGE, user)

	for(var/mob/ghost as anything in GLOB.dead_mob_list)
		name_stub = " (<b>[GET_GUESTBOOK_NAME(ghost, user)]</b>)"
		message_with_name = message + name_stub
		if((ghost.client?.prefs.chat_toggles & CHAT_GHOSTSIGHT) && !(ghost in viewers))
			to_chat(ghost, "[FOLLOW_LINK(ghost, user)] [span_emote(message_with_name)]")

	for(var/mob/receiver in viewers)
		name_stub = " (<b>[GET_GUESTBOOK_NAME(receiver, user)]</b>)"
		message_with_name = message + name_stub
		receiver.show_message(span_emote(message_with_name), alt_msg = span_emote(message_with_name))
		if (receiver.client?.prefs.read_preference(/datum/preference/toggle/enable_runechat))
			receiver.create_chat_message(user, null, message, null, EMOTE_MESSAGE)
